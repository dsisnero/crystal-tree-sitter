module TreeSitter
  record Capture, rule : String, node : Node, index : UInt32 do
    def includes_line?(line_n : Int32) : Bool
      node.start_point.row == line_n || node.end_point.row == line_n
    end

    def <(other : Int32) : Bool
      node.end_point.row < other && node.start_point.row < other
    end

    def to_s(io : IO)
      io << @rule << ' '
      node.to_s(io)
    end

    def text(source : String) : String
      node.text(source)
    end

    def inspect(io : IO)
      io << "#<Capture "
      to_s(io)
      io << " start="
      node.start_point.to_s(io)
      io << " end="
      node.end_point.to_s(io)
      io << '>'
    end
  end
end
